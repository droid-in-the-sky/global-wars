#ifndef _GAME_CEU
#define _GAME_CEU

#include "lua.ceu"
#include "sdl.ceu"
#include "sdl-gfx.ceu"

#include "map.ceu"
#include "game-human.ceu"

input void SDL_REDRAW;
input _SDL_KeyboardEvent* SDL_KEYDOWN;

class Game with
    var char[] file = null;
do
    _assert(_REN != null);

    _assert(this.file != null);
    [[ dofile(@this.file) ]];

    var Map map with
        this.ren := _REN;
    end;
    var MapFinger fgr with
        this.map = map;
    end;

    // while not finished

    loop do

        // current #states
        var int ns = [[ #STATES ]];

        // show current state
        do
            var GameState s with
                this.ren := _REN;
                this.map = map;
                this.idx = ns;
            end;

            var _char[15] str;
            _sprintf(str, " WEEK %d ", (ns+1)/2);
            var _SDL_Texture* tex;
                finalize
                    tex = _SDL_text2texture(_REN, _FONT_30, str,
                                            &_colors.black, &_colors.white);
                with
                    _SDL_DestroyTexture(tex);
                end
            var UITexture nxt with
                this.ren := _REN;
                this.tex := tex;
                this.bg_clr := &_colors.white;
                this.align_x = _UI_ALIGN_CENTER;
                this.align_y = _UI_ALIGN_MIDDLE;
            end;
            var _SDL_Rect r;
                r.x = _REN_W/2;
                r.y = _REN_H/2;
                r.w = _REN_W / 4;
                r.h = _REN_H / 4;
            call nxt.go(&r);

            await 2s;
        end

        // foreach player
        loop p_ in 6 do
            var int p = p_ + 1;     // C => Lua

            // none, AI or human?
            var bool none?  = [[ PLAYERS[@p] == false   ]];
            var bool human? = [[ PLAYERS[@p] == 'human' ]];
            var bool ai?    = [[ PLAYERS[@p] == 'ai'    ]];
            var bool net?   = [[ PLAYERS[@p] == 'net' ]];

            // NONE
            if none? then
                [[ SRV_move(@p, {}) ]];

            // HUMAN
            else/if human? then
                _assert(human?);
                do GameHuman with
                    this.map = map;
                    this.me  = p;
                end;

            // AI
            else/if ai? then
                [[ SRV_move(@p, AI_move(@ns,@p)) ]];

            // NET
/*
            else/if net? then
                par/or do
                    [[ SRV_move(@p, SRV_get(@ns,@p)) ]];
                with
                    // network blink
                    await FOREVER;
                end
*/

            // ERROR
            else
                _assert(0);
            end
        end

        // next turn
        [=[
            SRV_go()
            local f = assert(io.open(@this.file, 'w'))
            f:write([[
                dofile(__LUA_MAP)
                PLAYERS = ]]..table2string(PLAYERS)..[[
                STATES  = ]]..table2string(STATES)..[[
                MOVES   = ]]..table2string(MOVES)..[[
            ]])
            f:close()
        ]=];
    end
end

#ifdef __GAME_CEU

#include "all.ceu"

par/or do
    await SDL_QUIT;
/*** SIMUL
with
    @include(simul.ceu)
***/
with
    every SDL_REDRAW do
        _boxRGBA(_REN, 0,0, _REN_W,_REN_H, 0,0,0, 0xFF);
    end
with

#ifdef __ANDROID__
#define LUA_GAME \
    "/data/data/org.droid_in_the_sky.global_wars/lib/lib_lua__g.so"
#else
#define LUA_GAME   "_g1.lua"
#endif

    var Game game with
        this.file := LUA_GAME;
    end;
    await FOREVER;
with
    every SDL_REDRAW do
        _SDL_RenderPresent(_REN);
    end
end

escape 0;

#endif
#endif
