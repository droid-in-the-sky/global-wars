#ifndef _GAME_CEU
#define _GAME_CEU

#include "lua.ceu"
#include "sdl.ceu"
#include "sdl-gfx.ceu"

#include "map.ceu"
#include "game-human.ceu"

input void SDL_REDRAW;
input _SDL_KeyboardEvent* SDL_KEYDOWN;
input void SDL_MOUSEBUTTONDOWN;

class Game with
    var char[] file = null;
do
    _assert(_REN != null);

    _assert(this.file != null);
    [[ dofile(@this.file) ]];

    var Map map;
    var MapFinger fgr with
        this.map = map;
    end;

    // while not finished

    loop do

        // current #states
        var int ns = [[ #STATES ]];

        // foreach player
        loop p_ in 6 do
            var int p = p_ + 1;     // C => Lua

            // none, AI or human?
            var bool none? = [[ PLAYERS[@p] == false   ]];
            var bool ai?   = [[ PLAYERS[@p] == 'ai'    ]];
            var bool human?= [[ PLAYERS[@p] == 'human' ]];

            // NONE
            if none? then
                [[ SRV_move(@p, {}) ]];

            // AI
            else/if ai? then
                [[ SRV_move(@p, AI_move(@ns,@p)) ]];

            // HUMAN
            else
                _assert(human?);
                var GameHuman human with
                    this.map = map;
                    this.me  = p;
                end;
                await human.ok;
            end
        end

        // next turn
        [=[
            SRV_go()
            local f = assert(io.open(@this.file, 'w'))
            f:write([[
                dofile(LUA_MAP)
                PLAYERS = ]]..table2string(PLAYERS)..[[
                STATES  = ]]..table2string(STATES)..[[
                MOVES   = ]]..table2string(MOVES)..[[
            ]])
            f:close()
        ]=];
    end
end

#ifdef __GAME_CEU

#include "all.ceu"

par/or do
    await SDL_QUIT;
/*** SIMUL
with
    @include(simul.ceu)
***/
with
    every SDL_REDRAW do
        _boxRGBA(_REN, 0,0, _REN_W,_REN_H, 0,0,0, 0xFF);
    end
with

#ifdef __ANDROID__
#define LUA_GAME \
    "/data/data/org.droid_in_the_sky.global_wars/lib/lib_lua__g.so"
#else
#define LUA_GAME   "_g1.lua"
#endif

    var Game game with
        this.file := LUA_GAME;
    end;
    await FOREVER;
with
    every SDL_REDRAW do
        _SDL_RenderPresent(_REN);
    end
end

escape 0;

#endif
#endif
