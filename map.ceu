import lua.ceu;
import sdl.ceu;
import sdl-gfx.ceu;

import ui.ceu;
import colors.ceu;

input void SDL_QUIT;
input void SDL_REDRAW;
input _SDL_MouseButtonEvent* SDL_MOUSEBUTTONDOWN;

native pure _P(), _I();
native do
    int P (int p, int c, int dp, int zoom) {
        return (dp + p - c) * zoom / 100 + c;
    }
    int I (int i, int c, int dp, int zoom) {
        return (i - c) * 100 / zoom + c - dp;
    }
end

class Map with
    var int  n;   // number of countries
    var u16* cx;  // all country centers
    var u16* cy;  // all country centers

    event int go_zoom, go_x, go_y;
    event int ok_clicked;
do
    _assert(_REN != null);
    _assert(_L   != null);

    _lua_getglobal(_L, "MAP");     // [ M ]
    this.n = _lua_objlen(_L,1);
    _lua_pop(_L, 1);               // [ ]

    // cx / cy (pos n=0 unused, malloc 1 extra)
    finalize
        this.cx = _malloc((this.n+1) * sizeof(u16));
    with
        _free(this.cx);
    end
    finalize
        this.cy = _malloc((this.n+1) * sizeof(u16));
    with
        _free(this.cy);
    end

    var int zzz = 100;
    var int xxx = 0;
    var int yyy = 0;

    var int xc = _REN_W / 2;
    var int yc = _REN_H / 2;

    do
        var int xmin, xmax;
            _lua_getglobal(_L, "xs_min_max");  // [ f ]
            _lua_call(_L, 0, 2);               // [ xmin | xmax ]
            xmin = _lua_tointeger(_L, -2);
            xmax = _lua_tointeger(_L, -1);
            _lua_pop(_L, 2);                   // [ ]

        var int ymin, ymax;
            _lua_getglobal(_L, "ys_min_max");  // [ f ]
            _lua_call(_L, 0, 2);               // [ ymin | ymax ]
            ymin = _lua_tointeger(_L, -2);
            ymax = _lua_tointeger(_L, -1);
            _lua_pop(_L, 2);                   // [ ]

        var int  w = xmax - xmin;
        var int  h = ymax - ymin;
        var int dx = _REN_W - w;
        var int dy = _REN_H - h;

        // best fit
        if dx < dy then
            zzz = 100*_REN_W / w;
        else
            zzz = 100*_REN_H / h;
        end

        // center on x / y
        xxx = dx/2 - xmin;
        yyy = dy/2 - ymin;
    end

    par do
        loop do
            // center
            _lua_getglobal(_L, "MAP");         // [ M ]
            loop i_, this.n do
                var int i = i_ + 1;     // C => Lua indexing
                _lua_rawgeti(_L, 1, i);            // [ M | C ]
                _lua_getfield(_L, -1, "center");   // [ M | C | pt ]
                _lua_rawgeti(_L, -1, 1);           // [ M | C | pt | x ]
                _lua_rawgeti(_L, -2, 2);           // [ M | C | pt | x | y ]
                this.cx[i] = _P(_lua_tointeger(_L,-2), xc, xxx, zzz);
                this.cy[i] = _P(_lua_tointeger(_L,-1), yc, yyy, zzz);
                _lua_pop(_L, 4);                   // [ ]
            end
            _lua_pop(_L, 1);                   // [ ]

            par/or do
                var int v = await go_zoom;      // ZOOM
                zzz = zzz + v;
                if zzz < 1 then
                    zzz = 1;
                end
            with
                var int v = await go_x;         // PAN X
                xxx = xxx + v;
            with
                var int v = await go_y;         // PAN Y
                yyy = yyy + v;
            end
        end
    with
        // OK_CLICKED
        var _SDL_MouseButtonEvent* but;
        every but = SDL_MOUSEBUTTONDOWN do
            var int x = _I(but:x, xc, xxx, zzz);
            var int y = _I(but:y, yc, yyy, zzz);
            _lua_getglobal(_L, "MAP_vspoint"); // [ f ]
            _lua_pushnumber(_L, x);            // [ f | x ]
            _lua_pushnumber(_L, y);            // [ f | x | y ]
            _lua_call(_L, 2, 1);               // [ c ]
            var int c = _lua_tointeger(_L, -1);
            _lua_pop(_L, 1);                   // [ ]
            if c > 0 then
                emit this.ok_clicked => c;
            end
        end
    with
        every SDL_REDRAW do
            var int n = 1;      // traverses xs/ys continuously

            _lua_getglobal(_L, "MAP");        // [ M | T ]
            _lua_getglobal(_L, "TMAP");       // [ M | T ]
                _lua_getfield(_L, -1, "ns");  // [ M | T | ns ]
                _lua_getfield(_L, -2, "xs");  // [ M | T | ns | xs ]
                _lua_getfield(_L, -3, "ys");  // [ M | T | ns | xs | ys ]

            // for each country [0, #MAP)
            loop i_, this.n do
                var int i = i_ + 1;     // C => Lua indexing
                var u16[10] xs, ys;     // TODO: negative values?

                _lua_rawgeti(_L, 3, i);       // [ . | ns[i] ]
                var int pts = _lua_tointeger(_L, -1);
                _lua_pop(_L, 1);              // [ . ]

                // for each point [0, ns[i])
                loop j, pts do
                    _lua_rawgeti(_L, 4, n);   // [ . | xs[n] ]
                    _lua_rawgeti(_L, 5, n);   // [ . | xs[n] | ys[n] ]
                    xs[j] = _P(_lua_tointeger(_L,-2), xc, xxx, zzz);
                    ys[j] = _P(_lua_tointeger(_L,-1), yc, yyy, zzz);
                    _lua_pop(_L, 2);          // [ . ]
                    n = n + 1; // traverses xs/ys continuously
                end

                // push color
                _lua_rawgeti(_L, 1, i);         // [ . | C ]
                _lua_getfield(_L, -1, "color"); // [ . | C | clr ]
                _lua_rawgeti(_L, -1, 1);        // [ . | C | clr | r ]
                _lua_rawgeti(_L, -2, 2);        // [ . | C | clr | r | g ]
                _lua_rawgeti(_L, -3, 3);        // [ . | C | clr | r | g | b ]

                // draw country with color
                _filledPolygonRGBA(_REN, xs, ys, pts,
                    _lua_tointeger(_L, -3),
                    _lua_tointeger(_L, -2),
                    _lua_tointeger(_L, -1),
                    0xFF);

                // draw country border
                _polygonColor(_REN, xs, ys, pts, _colors.black);

                // pop color
                _lua_pop(_L, 5);                   // [ . ]

                // draw center
                //_boxColor(_REN, cx[i]-1,cy[i]-1, cx[i]+1,cy[i]+1, 
                //_colors.black);
            end

            _lua_pop(_L, 5);               // [ ]

            // for each border { {p1,p2}, {p3,p4}, ... }
            _lua_getglobal(_L, "BORDERS"); // [ B ]
            loop i_, _lua_objlen(_L,1) do
                var int i = i_ + 1;     // C => Lua indexing
                _lua_rawgeti(_L, 1, i);    // [ B | t ]
                _lua_rawgeti(_L, -1, 1);   // [ B | t | p1 ]
                _lua_rawgeti(_L, -2, 2);   // [ B | t | p1 | p2 ]
                _lua_rawgeti(_L, -2, 1);   // [ B | t | p1 | p2 | x1 ]
                _lua_rawgeti(_L, -3, 2);   // [ B | t | p1 | p2 | x1 | y1 ]
                _lua_rawgeti(_L, -3, 1);   // [ B | t | p1 | p2 | x1 | y1 | x2 ]
                _lua_rawgeti(_L, -4, 2);   // [ B | t | p1 | p2 | x1 | y1 | x2 | y2 ]
                _lineRGBA(_REN,
                    _P(_lua_tointeger(_L,-4), xc, xxx, zzz),
                    _P(_lua_tointeger(_L,-3), yc, yyy, zzz),
                    _P(_lua_tointeger(_L,-2), xc, xxx, zzz),
                    _P(_lua_tointeger(_L,-1), yc, yyy, zzz),
                    0,0,0, 0xFF);
                _lua_pop(_L, 7);           // [ B ]
            end
            _lua_pop(_L, 1);               // [ ]
        end
    end
end

native do
    lua_State*    L   = NULL;
    SDL_Renderer* REN = NULL;
    int REN_W, REN_H;
end

var _SDL_Window* win;
    finalize
        win = _SDL_CreateWindow("Map",
                            500, 400, 800, 480, _SDL_WINDOW_SHOWN);
    with
        _SDL_DestroyWindow(win);
    end
_SDL_GetWindowSize(win, &_REN_W, &_REN_H);

_REN = _SDL_CreateRenderer(win, -1, 0);
finalize with
    _SDL_DestroyRenderer(_REN);
end

_L = _luaL_newstate();
finalize with
    _lua_close(_L);
end
_lua_atpanic(_L, _ceu_lua_atpanic);
_luaL_openlibs(_L);

par/or do
    await SDL_QUIT;
/*** SIMUL
with
    @include(simul.ceu)
***/
with
    every SDL_REDRAW do
        _boxColor(_REN, 0,0, _REN_W,_REN_H, _colors.black);
    end
with
    do
        _luaL_loadfile(_L, "map1.lua");  // [ f ]
        _lua_call(_L, 0, 0);             // [ ]
        var Map map;
        await 2s;
    end

    _luaL_loadfile(_L, "map2.lua");  // [ f ]
    _lua_call(_L, 0, 0);             // [ ]
    var Map map;
    await 2s;

    par do
        loop i, 200 do
            await 50ms;
            if i % 2 == 0 then
                emit map.go_x => -1;
            else
                emit map.go_y => -1;
            end
        end
    with
        loop i, 200 do
            await 51ms;
            emit map.go_zoom => -1;
        end
        loop do
            await 101ms;
            emit map.go_zoom => 1;
        end
    end
with
    every SDL_REDRAW do
        _SDL_RenderPresent(_REN);
    end
end

return 0;

